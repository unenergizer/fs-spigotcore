import com.sun.org.apache.xalan.internal.xsltc.cmdline.Compile


// ========================= The basics ========================= //

group = 'com.forgestorm.spigotcore'
version = '0.0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Manifest
jar {
    manifest.attributes provider: 'gradle'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Force UTF-8
tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

// ========================= Clean extension ========================= //
// Removes the folder out/ which is would have otherwise been left in
// the root directory, if it wasn't for this method.

clean.doFirst {
    delete "$rootDir/out/"
}

// ========================= Custom compilation task ========================= //
// Bundles all of the compiled jars into a nice and sane directory. (/out/)

tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

// ========================= Provided scope ========================= //
// The provided scope, allows the developer to choose which dependencies
// will be shaded into the jar file. Provided tells the compiler that the
// dependency is provided at runtime and should therefor not be compiled
// while compile tells the compiler that it is needed right here and now
// and should therefor be compiled into the jar.

configurations {
    provided
    compile.extendsFrom provided
}

artifacts {
    archives jar
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

sourceSets {
    main {
        output.resourcesDir = "build/classes/main"
    }
    test {
        output.resourcesDir = "build/classes/test"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// ========================= Repositories ========================= //

repositories {
    mavenCentral()
    jcenter()

//    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } // Giving PaperSpigot a try. If it sucks, uncomment :)
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }

    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }

    maven {
        name 'destroystokyo-repo'
        url 'https://repo.destroystokyo.com/repository/maven-public/'
    }

    maven {
        name 'puharesource-repo'
        url 'http://repo.puha.io/nexus/content/repositories/releases/'
    }

    maven {
        url 'https://jitpack.io'
    }

    maven {
        name 'dmulloy2-repo'
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }

    maven {
        name 'citizensapi'
        url 'http://repo.citizensnpcs.co/'
    }

    maven {
        name 'BungeeCord'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

//    maven {
//        name 'sk89q-snapshots'
//        url 'https://maven.sk89q.com/repo'
//    }
}

// ========================= Dependencies ========================= //

dependencies {

    compile 'net.dv8tion:JDA:3.7.1_395'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.3.2'
    compile group: 'io.puharesource.mc', name: 'TitleManager', version: '2.0.0'
    compile "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    compile group: 'org.zeroturnaround', name: 'zt-zip', version: '1.11'
    compile group: 'org.apache.ant', name: 'ant-commons-net', version: '1.10.0'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'com.github.koca2000', name: 'NoteBlockAPI', version: '-SNAPSHOT'

    compileOnly 'com.destroystokyo.paper:paper-api:1.12-R0.1-SNAPSHOT'

    provided group: 'net.md-5', name: 'bungeecord-api', version: '1.10-SNAPSHOT'
    provided group: 'net.citizensnpcs', name: 'citizensapi', version: '2.0.23-SNAPSHOT'
    provided group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.3.1-SNAPSHOT'
    provided group: 'LibsDisguises', name: 'LibsDisguises', version: '9.5.2-SNAPSHOT'
    provided 'org.projectlombok:lombok:1.18.2'

    provided files("src/main/Libs/Item-NBT-API.jar")

    //compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT' // Giving PaperSpigot a try. If it sucks, uncomment :)
    //provided group: 'com.sk89q', name: 'worldedit', version: '6.0.0-SNAPSHOT'
}